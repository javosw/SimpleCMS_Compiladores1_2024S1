
package josq.cms.lenguajes.parser;

// antes de la clase parser

import java.util.ArrayList;
//import java_cup.runtime.*;
//import josq.cms.lenguajes.modelos.arbol.*;
//import josq.cms.lenguajes.modelos.Indicador;
import josq.cms.lenguajes.modelos.Indicador;
import josq.cms.lenguajes.modelos.cup.Accion;
import josq.cms.lenguajes.modelos.cup.simbolos.Par;
import josq.cms.lenguajes.modelos.cup.simbolos.Atributo;
import josq.cms.lenguajes.modelos.cup.simbolos.Parametro;

// NOTA: %cupsym en jflex
// parser=MyParser, sym=MyParserSym
class ParserAcciones;

// dentro de la clase parser
// parser code {: :}

// metodo user_init() de la clase parser generada
// init with {: :}

// metodo scan() de la clase parser generada
// scan with {: :}

// dentro de la clase action
action code {:
    void print(String texto){ System.out.print(texto); } 

:}

// #%#%#%#% SIMBOLOS #%#%#%#%
// non terminal Type2 mySymbol1, mySymbol2 ;

// #%#%#%#% TERMINALES #%#%#%#%

// puntuacion
terminal IZQ, DER, BARRA, IGUAL, IZQCOR, DERCOR, COMI, OR ;
// nodos
terminal ACCIS, ACCI, PARAMS, PARAM, ATRIBS, ATRIB, ETIQS, ETIQ ;
// nodo.atributo
terminal VALOR, NOMBRE ;
// acciones.nombre
terminal SITE_NEW, SITE_DEL, PAGE_NEW, PAGE_DEL, PAGE_MOD, COMP_NEW, COMP_DEL, COMP_MOD ;
// parametro.nombre
terminal P_ID, P_TITULO, P_SITIO, P_PADRE, P_PAGINA, P_CLASE ;
terminal P_FECHA_NEW, P_FECHA_MOD, P_USER_NEW, P_USER_MOD ;
// parametro.nombre.clase
terminal UI_TITULO, UI_PARRAFO, UI_IMAGEN, UI_VIDEO, UI_MENU ;
// atributo.nombre
terminal A_TEXTO, A_ALIGN, A_COLOR, A_ORIGEN, A_ALTO, A_ANCHO, A_PADRE, A_ETIQS ;
// atributo.nombre.alineacion
terminal T_CENTRAR, T_IZQUIERDA, T_DERECHA, T_JUSTIFICAR ;
// literales
terminal String MI_ID, MI_TEXTO, MI_NUMERO, MI_COLOR, MI_URL, MI_FECHA, MI_ETIQUETA ;

// #%#%#%#% NO TERMINALES #%#%#%#%
non terminal ArrayList<Accion> cms, acciones, accionesMID ;
non terminal accionesIZQ, accionesDER ;
non terminal Accion accion, accionMID ;
non terminal Indicador accionIZQ, accionNombre, accionTipo ; 
non terminal accionDER ;
non terminal ArrayList<Parametro> parametros, parametrosMID ;
non terminal parametrosIZQ, parametrosDER ;
non terminal Parametro parametro ;
non terminal Indicador parametroIZQ, parametroNombre, parametroTipo ;
non terminal Par parametroMID, parametroContent ;
non terminal parametroDER ;
non terminal ArrayList<Atributo> atributos, atributosMID ;
non terminal atributosIZQ, atributosDER ;
non terminal Atributo atributo ;
non terminal Indicador atributoIZQ, atributoNombre, atributoTipo ;
non terminal Par atributoMID, atributoContent ;
non terminal atributoDER ;
non terminal ArrayList<String> etiquetas, etiquetasMID ;
non terminal etiquetasIZQ, etiquetasDER ;
non terminal String etiqueta, etiquetaValor ; 
non terminal Indicador miAlineacion ;
non terminal ArrayList<String> misEtiquetas ;
non terminal Indicador miComponente ;

// PRODUCCIONES

// definicion de la produccion inicial
start with cms ;

// INICIO
cms ::= 
    accion:w1  {: ArrayList<Accion> list = new ArrayList<>(); if(w1!=null) list.add(w1); RESULT=list; :} // +
    | acciones:w1  {: RESULT=w1; :}
    | error:w1 {: print("@ERROR("+w1left+"-"+w1right+") "); :}
;

// ACCIONES
acciones ::=
    accionesIZQ accionesMID:w1 accionesDER  {: RESULT=w1; :} // +
    | error:w1 DER {: print("@ERROR("+w1left+"-"+w1right+") "); :} // +
;
accionesIZQ ::=
    IZQ ACCIS DER // +
;
accionesMID ::=
    accion:w1 {: ArrayList<Accion> list = new ArrayList<>(); if(w1!=null) list.add(w1); RESULT=list; :} // +
    | accionesMID:w1 accion:w2  {: if(w2!=null) w1.add(w2); RESULT=w1; :} // +
;
accionesDER ::=
    IZQ BARRA ACCIS DER // +
;

// ACCION
accion ::=
    accionIZQ:w1 accionMID:w2 accionDER  {: w2.setTipo(w1); RESULT=w2; :} // +
;
accionIZQ ::=
    IZQ ACCI accionNombre:w1 DER  {: RESULT=w1; :} //+
;
accionNombre ::=
    NOMBRE IGUAL COMI accionTipo:w1 COMI {: RESULT=w1; :} //+
;
accionTipo ::=
    SITE_NEW:w1   {: RESULT=Indicador.SITE_NEW; :}  // +
    | SITE_DEL:w1 {: RESULT=Indicador.SITE_DEL; :}  // +
    | PAGE_NEW:w1 {: RESULT=Indicador.PAGE_NEW; :}  // +
    | PAGE_DEL:w1 {: RESULT=Indicador.PAGE_DEL; :}  // +
    | PAGE_MOD:w1 {: RESULT=Indicador.PAGE_MOD; :}  // +
    | COMP_NEW:w1 {: RESULT=Indicador.COMP_NEW; :}  // +
    | COMP_DEL:w1 {: RESULT=Indicador.COMP_DEL; :}  // +
    | COMP_MOD:w1 {: RESULT=Indicador.COMP_MOD; :}  // +
;
accionMID ::= 
    parametros:w1  {: Accion a = new Accion(); a.setParametros(w1); RESULT=a; :} // +
    | atributos:w1 {: Accion a = new Accion(); a.setAtributos(w1); RESULT=a; :} // +
    | etiquetas:w1 {: Accion a = new Accion(); a.setEtiquetas(w1); RESULT=a; :} // +
    | accionMID:w1 parametros:w2 {: w1.setParametros(w2); RESULT=w1; :} // +
    | accionMID:w1 atributos:w2  {: w1.setAtributos(w2); RESULT=w1; :} // +
    | accionMID:w1 etiquetas:w2  {: w1.setEtiquetas(w2); RESULT=w1; :} // +
    | error:w1 DER {: print("@ERROR("+w1left+"-"+w1right+") "); :} // +
;
accionDER ::=
    IZQ BARRA ACCI DER //+
;

// PARAMETROS
parametros ::= 
    parametrosIZQ parametrosMID:w1 parametrosDER  {: RESULT=w1; :} // +
;
parametrosIZQ ::=
    IZQ PARAMS DER // +
;
parametrosMID ::=
    parametro:w1  {: ArrayList<Parametro> list = new ArrayList<>(); if(w1!=null) list.add(w1); RESULT=list; :} // +
    | parametrosMID:w1 parametro:w2 {: if(w2!=null) w1.add(w2); RESULT=w1; :} // +
;
parametrosDER ::=
    IZQ BARRA PARAMS DER // +
;

// PARAMETRO
parametro ::=
    parametroIZQ:w1 parametroMID:w2 parametroDER  {: RESULT= new Parametro(w1,w2); :} // +
;
parametroIZQ ::=
    IZQ PARAM parametroNombre:w1 DER  {: RESULT=w1; :} // +
;
parametroNombre ::=
    NOMBRE IGUAL COMI parametroTipo:w1 COMI {: RESULT=w1; :} // +
;
parametroTipo ::=
    P_ID          {: RESULT=Indicador.P_ID; :}        // +
    | P_TITULO    {: RESULT=Indicador.P_TITULO; :}    // +
    | P_SITIO     {: RESULT=Indicador.P_SITIO; :}     // +
    | P_PADRE     {: RESULT=Indicador.P_PADRE; :}     // +
    | P_PAGINA    {: RESULT=Indicador.P_PAGINA; :}    // +
    | P_CLASE     {: RESULT=Indicador.P_CLASE; :}     // +
    | P_USER_NEW  {: RESULT=Indicador.P_USER_NEW; :}  // +
    | P_USER_MOD  {: RESULT=Indicador.P_USER_MOD; :}  // +
    | P_FECHA_NEW {: RESULT=Indicador.P_FECHA_NEW; :} // +
    | P_FECHA_MOD {: RESULT=Indicador.P_FECHA_MOD; :} // +
;
parametroMID ::=
    IZQCOR parametroContent:w1 DERCOR {: RESULT=w1; :} // +
;
parametroContent ::=
    MI_ID:w1          {: RESULT= new Par(Indicador.MI_ID,w1); :}         // +
    | MI_TEXTO:w1     {: RESULT= new Par(Indicador.MI_TEXTO,w1); :}      // +
    | MI_FECHA:w1     {: RESULT= new Par(Indicador.MI_FECHA,w1); :}      // +
    | miComponente:w1 {: RESULT= new Par(Indicador.MI_COMPONENTE,w1); :} // +
;
miComponente ::=
    | UI_TITULO  {: RESULT=Indicador.UI_TITULO; :}  // + 
    | UI_PARRAFO {: RESULT=Indicador.UI_PARRAFO; :} // + 
    | UI_IMAGEN  {: RESULT=Indicador.UI_IMAGEN; :}  // + 
    | UI_VIDEO   {: RESULT=Indicador.UI_VIDEO; :}   // + 
    | UI_MENU    {: RESULT=Indicador.UI_MENU; :}    // + 
;
parametroDER ::=
    IZQ BARRA PARAM DER // +
;

// ATRIBUTOS
atributos ::=
    atributosIZQ atributosMID:w1 atributosDER  {: RESULT=w1; :} // +
;
atributosIZQ ::=
    IZQ ATRIBS DER // +
;
atributosMID ::=
    atributo:w1  {: ArrayList<Atributo> lista = new ArrayList<>(); if(w1!=null) lista.add(w1); RESULT=lista; :} // +
    | atributosMID:w1 atributo:w2  {: if(w2!=null) w1.add(w2); RESULT=w1; :} // +
;
atributosDER ::=
    IZQ BARRA ATRIBS DER // +
;

// ATRIBUTO
atributo ::=
    atributoIZQ:w1 atributoMID:w2 atributoDER  {: RESULT= new Atributo(w1,w2); :} // +
;
atributoIZQ ::=
    IZQ ATRIB atributoNombre:w1 DER  {: RESULT=w1; :} // +
;
atributoNombre ::=
    NOMBRE IGUAL COMI atributoTipo:w1 COMI {: RESULT=w1; :} // +
;
atributoTipo ::=
    A_TEXTO    {: RESULT=Indicador.A_TEXTO; :}  // +
    | A_ALIGN  {: RESULT=Indicador.A_ALIGN; :}  // +
    | A_COLOR  {: RESULT=Indicador.A_COLOR; :}  // +
    | A_ORIGEN {: RESULT=Indicador.A_ORIGEN; :} // +
    | A_ALTO   {: RESULT=Indicador.A_ALTO; :}   // +
    | A_ANCHO  {: RESULT=Indicador.A_ANCHO; :}  // +
    | A_PADRE  {: RESULT=Indicador.A_PADRE; :}  // +
    | A_ETIQS  {: RESULT=Indicador.A_ETIQS; :}  // +
;
atributoMID ::=
    IZQCOR atributoContent:w1 DERCOR {: RESULT=w1; :} // +
;
atributoContent ::= 
    MI_ID:w1          {: RESULT= new Par(Indicador.MI_ID,w1); :}         // +
    | MI_TEXTO:w1     {: RESULT= new Par(Indicador.MI_TEXTO,w1); :}      // +
    | MI_COLOR:w1     {: RESULT= new Par(Indicador.MI_COLOR,w1); :}      // +
    | MI_URL:w1       {: RESULT= new Par(Indicador.MI_URL,w1); :}        // +
    | MI_NUMERO:w1    {: RESULT= new Par(Indicador.MI_NUMERO,w1); :}     // +
    | misEtiquetas:w1 {: RESULT= new Par(Indicador.MIS_ETIQUETAS,w1); :} // +
    | miAlineacion:w1 {: RESULT= new Par(Indicador.MI_ALIGN,w1); :}      // +
;
miAlineacion ::=
    T_CENTRAR      {: RESULT=Indicador.T_CENTRAR; :}    // +
    | T_DERECHA    {: RESULT=Indicador.T_DERECHA; :}    // +
    | T_IZQUIERDA  {: RESULT=Indicador.T_IZQUIERDA; :}  // +
    | T_JUSTIFICAR {: RESULT=Indicador.T_JUSTIFICAR; :} // +
;
misEtiquetas ::=
    MI_ETIQUETA:w1 {: ArrayList<String> list = new ArrayList<>(); if(w1!=null) list.add(w1); RESULT=list; :} // +
    | misEtiquetas:w1 OR MI_ETIQUETA:w2 {: if(w2!=null) w1.add(w2); RESULT=w1; :} // +
;
atributoDER ::=
    IZQ BARRA ATRIB DER // +
;

// ETIQUETAS
etiquetas ::=
    etiquetasIZQ etiquetasMID:w1 etiquetasDER  {: RESULT=w1; :} // +
;
etiquetasIZQ ::=
    IZQ ETIQS DER // +
;
etiquetasMID ::= 
    etiqueta:w1 {: ArrayList<String> list = new ArrayList<>(); if(w1!=null) list.add(w1); RESULT=list; :} // +
    | etiquetasMID:w1 etiqueta:w2 {: if(w2!=null) w1.add(w2); RESULT=w1; :} // +
;
etiqueta ::= 
    IZQ ETIQ etiquetaValor:w1 BARRA DER  {: RESULT=w1; :} // +
;
etiquetaValor ::=
    VALOR IGUAL COMI MI_ETIQUETA:w1 COMI  {: RESULT=w1; :} // +
;
etiquetasDER ::=
    IZQ BARRA ETIQS DER // +
;


